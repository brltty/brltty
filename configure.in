dnl Process this file with autoconf 2.53 to produce a configure script.
###############################################################################
# BRLTTY - A background process providing access to the console screen (when in
#          text mode) for a blind person using a refreshable braille display.
#
# Copyright (C) 1995-2006 by The BRLTTY Developers.
#
# BRLTTY comes with ABSOLUTELY NO WARRANTY.
#
# This is free software, placed under the terms of the
# GNU General Public License, as published by the Free Software
# Foundation.  Please see the file COPYING for details.
#
# Web Page: http://mielke.cc/brltty/
#
# This software is maintained by Dave Mielke <dave@mielke.cc>.
###############################################################################

AC_PREREQ([2.53])
AC_INIT([brltty], [3.8dev], [http://mielke.cc/brltty/])
AC_CONFIG_SRCDIR([Programs/brltty.h])
BRLTTY_SUMMARY_BEGIN
brltty_build_directories=". Programs Documents"

brltty_manual_directories=""
for brltty_manual_directory in Documents/Manual-*
do
   for brltty_language_directory in ${brltty_manual_directory}/*
   do
      test -f "${brltty_language_directory}/Makefile.in" && {
         brltty_manual_directories="${brltty_manual_directories} ${brltty_language_directory}"
      }
   done
done
AC_SUBST([brltty_manual_directories])
brltty_build_directories="${brltty_build_directories} ${brltty_manual_directories}"

AC_SUBST([api_major], [0])
AC_SUBST([api_minor], [5])
AC_SUBST([api_revision], [0])
AC_SUBST([api_version], [${api_major}.${api_minor}])
AC_SUBST([api_release], [${api_version}.${api_revision}])
AC_DEFINE_UNQUOTED([BRLAPI_RELEASE], ["${api_release}"],
                   [Define this to be a string containing the API library version.])

AC_SUBST([PACKAGE_TITLE], ["`echo "${PACKAGE_NAME}" | sed -e 'y%abcdefghijklmnopqrstuvwxyz%ABCDEFGHIJKLMNOPQRSTUVWXYZ%'`"])
AC_DEFINE_UNQUOTED([PACKAGE_TITLE], ["${PACKAGE_TITLE}"],
                   [Define this to be a string containing the package title.])

AC_SUBST([package_date], ["`date "+%Y/%m/%d"`"])

AC_CONFIG_HEADER([config.h])
AC_CONFIG_HEADER([Programs/api.h])
AC_PREFIX_DEFAULT([])
AC_CONFIG_AUX_DIR([acdir])
AC_CANONICAL_SYSTEM

brltty_default_execute_root="/${PACKAGE_NAME}-${PACKAGE_VERSION}"
BRLTTY_ARG_WITH(
   [execute-root], [DIRECTORY],
   [where the package is to be anchored at run-time],
   [execute_root], [""]
)
if test "${execute_root}" = "no"
then
   execute_root=""
elif test "${execute_root}" = "yes"
then
   execute_root="${brltty_default_execute_root}"
fi
AC_SUBST([execute_root])
BRLTTY_SUMMARY_ITEM([execute-root], [execute_root])

brltty_default_install_root="/tmp/${PACKAGE_NAME}-${PACKAGE_VERSION}"
BRLTTY_ARG_WITH(
   [install-root], [DIRECTORY],
   [where to install the package],
   [install_root], ["${execute_root}"]
)
if test "${install_root}" = "no"
then
   install_root="${execute_root}"
elif test "${install_root}" = "yes"
then
   install_root="${brltty_default_install_root}"
fi
AC_SUBST([install_root])
BRLTTY_SUMMARY_ITEM([install-root], [install_root])

AC_SUBST([configuration_file], ["${PACKAGE_NAME}.conf"])
BRLTTY_DEFINE_EXPANDED([CONFIGURATION_FILE], ["${configuration_file}"],
                       [Define this to be a string containing the name of the default configuration file.])

BRLTTY_PORTABLE_DIRECTORY([includedir], [/usr])
BRLTTY_PORTABLE_DIRECTORY([mandir], [/usr/share])

BRLTTY_SUMMARY_ITEM([libdir], [libdir])
BRLTTY_SUMMARY_ITEM([sysconfdir], [sysconfdir])

BRLTTY_ARG_REQUIRED(
   [program-directory], [DIRECTORY],
   [path to directory containing executables],
   [program_directory], ["${bindir}"]
)

BRLTTY_ARG_REQUIRED(
   [library-directory], [DIRECTORY],
   [path to directory containing drivers],
   [library_directory], ["${libdir}/${PACKAGE_NAME}"]
)

BRLTTY_ARG_REQUIRED(
   [data-directory], [DIRECTORY],
   [path to directory containing tables and help pages],
   [data_directory], ["${sysconfdir}/${PACKAGE_NAME}"]
)

BRLTTY_ARG_REQUIRED(
   [manpage-directory], [DIRECTORY],
   [path to directory containing manual pages],
   [manpage_directory], ["${mandir}"]
)

BRLTTY_ARG_REQUIRED(
   [include-directory], [DIRECTORY],
   [path to directory containing header files],
   [include_directory], ["${includedir}/${PACKAGE_NAME}"]
)

original_prefix="${prefix}"
test "${prefix}" = "NONE" && prefix=""
original_exec_prefix="${exec_prefix}"
test "${exec_prefix}" = "NONE" && exec_prefix="${prefix}"
BRLTTY_DEFINE_DIRECTORY([DATA_DIRECTORY], [${execute_root}${data_directory}],
                        [Define this to be a string containing the desired working directory.])
BRLTTY_DEFINE_DIRECTORY([LIBRARY_DIRECTORY], [${execute_root}${library_directory}],
                        [Define this to be a string containing the path to the library directory.])
BRLTTY_DEFINE_DIRECTORY([CONFIGURATION_DIRECTORY], [${sysconfdir}],
                        [Define this to be a string containing the path to the configuration directory.])
BRLTTY_DEFINE_EXPANDED([BRLAPI_ETCDIR], ["${CONFIGURATION_DIRECTORY}"],
                       [Define this to be a string containing the path to the directory where the authorization key and driver-dependent key names are found.])
prefix="${original_prefix}"
exec_prefix="${original_exec_prefix}"

brltty_default_compiler_prefix="${host_os}-"
BRLTTY_ARG_WITH(
   [compiler-prefix], [PREFIX],
   [prefix for compiler programs],
   [compiler_prefix], [""]
)
if test "${compiler_prefix}" = "no"
then
   compiler_prefix=""
elif test "${compiler_prefix}" = "yes"
then
   compiler_prefix="${brltty_default_compiler_prefix}"
fi
AC_SUBST([compiler_prefix])
BRLTTY_SUMMARY_ITEM([compiler-prefix], [compiler_prefix])

brltty_default_init_path=/sbin/init_real
BRLTTY_ARG_WITH(
   [init-path], [FILE],
   [path to real init program],
   [init_path], [""]
)
if test "${init_path}" = "no"
then
   init_path=""
elif test "${init_path}" = "yes"
then
   init_path="${brltty_default_init_path}"
fi
if test -n "${init_path}"
then
   AC_DEFINE_UNQUOTED([INIT_PATH], ["${init_path}"],
                      [Define this if BRLTTY is to be run as init.])
fi
BRLTTY_SUMMARY_ITEM([init-path], [init_path])

AC_PROG_MAKE_SET

AC_PROG_CC
if test -z "${CXX}"
then
   if test "${GCC}" = "yes"
   then
      AC_SEARCH_LIBS([__cxa_pure_virtual], [supc++])
      AC_CACHE_CHECK([if the C compiler can compile C++], [brltty_cv_prog_cc_cxx], [dnl
      brltty_save_ac_ext="${ac_ext}"
      ac_ext="cc"
      AC_TRY_LINK([], [class c {};],
                  [brltty_cv_prog_cc_cxx="yes"],
                  [brltty_cv_prog_cc_cxx="no"])
      ac_ext="${brltty_save_ac_ext}"])
      test "${brltty_cv_prog_cc_cxx}" = "yes" && CXX="${CC}"
   fi
fi
AC_PROG_CXX

brltty_gcc_options="-Wall"
case "${build_os} ${host_os}"
in
   cygwin*\ mingw*)
      brltty_gcc_options="${brltty_gcc_options} -mno-cygwin"
      ;;
esac
if test "${GCC}" = "yes"
then
   CC="${CC} ${brltty_gcc_options}"
fi
if test "$GXX" = "yes"
then
   CXX="${CXX} ${brltty_gcc_options}"
fi

AC_CHECK_PROGS([LD], [ld], [ld])
AC_PROG_RANLIB
AC_PROG_YACC
AC_PROG_AWK
AC_PROG_LN_S

AC_PROG_INSTALL
BRLTTY_EXECUTABLE_PATH([INSTALL])

AC_CACHE_CHECK([for make relocatable object command], [brltty_cv_prog_mkobj], [dnl
case "${host_os}"
in
   *)
      brltty_cv_prog_mkobj="\$(LD) -r -o"
      ;;
esac])
MKOBJ="${brltty_cv_prog_mkobj}"
AC_SUBST([MKOBJ])

AC_CACHE_CHECK([for loadable module creation command], [brltty_cv_prog_mkmod], [dnl
case "${host_os}"
in
   linux*|gnu*|openbsd*|freebsd*|netbsd*|*qnx*|cygwin*|mingw*)
      brltty_mkmod_ld_make="-shared"
      ;;
   solaris*)
      brltty_mkmod_ld_make="-G"
      ;;
   hpux*)
      brltty_mkmod_ld_make="-b"
      ;;
   osf*)
      brltty_mkmod_ld_make="-shared"
      ;;
   darwin*)
      brltty_mkmod_ld_make="-bundle"
      brltty_mkmod_ld_options="-flat_namespace -undefined suppress"
      brltty_mkmod_gcc_make="-bundle"
      ;;
   msdos*)
      brltty_mkmod_ld_make="not_supported_on_msdos"
      brltty_mkmod_ld_options="not_supported_on_msdos"
      brltty_mkmod_gcc_make="not_supported_on_msdos"
      ;;
   *)
      AC_MSG_ERROR([loadable module creation command not configured for ${host_os}])
      ;;
esac
if test "${GCC}" = "yes"
then
   brltty_cv_prog_mkmod="\$(CC) ${brltty_mkmod_gcc_make=-shared} BRLTTY_OPTIONS_LD2CC([${brltty_mkmod_ld_options}]) -o"
else
   brltty_cv_prog_mkmod="\$(LD) ${brltty_mkmod_ld_make} ${brltty_mkmod_ld_options} -o"
fi])
AC_SUBST([MKMOD], ["${brltty_cv_prog_mkmod}"])

AC_CACHE_CHECK([for dynamic library creation command], [brltty_cv_prog_mklib], [dnl
case "${host_os}"
in
   linux*|gnu*|openbsd*|freebsd*|netbsd*|*qnx*)
      brltty_mklib_ld_make="-shared"
      brltty_mklib_ld_options="-soname"
      ;;
   solaris*)
      brltty_mklib_ld_make="-G"
      brltty_mklib_ld_options="-h"
      ;;
   hpux*)
      brltty_mklib_ld_make="-b"
      brltty_mklib_ld_options="+h"
      ;;
   osf*)
      brltty_mklib_ld_make="-shared"
      brltty_mklib_ld_options="-expect_unresolved '*' -soname"
      ;;
   cygwin*|mingw*)
      brltty_mklib_ld_make="-shared"
      brltty_mklib_ld_options="--out-implib implib.a -soname"
      ;;
   msdos*)
      brltty_mklib_ld_make="not_supported_on_msdos"
      brltty_mklib_ld_options="not_supported_on_msdos"
      brltty_mklib_gcc_make="not_supported_on_msdos"
      ;;
   darwin*)
      brltty_mklib_ld_make="-dylib"
      brltty_mklib_ld_options="-single_module -install_name"
      brltty_mklib_gcc_make="-dynamiclib"
      ;;
   *)
      AC_MSG_ERROR([dynamic library creation command not configured for ${host_os}])
      ;;
esac
if test "${GCC}" = "yes"
then
   brltty_cv_prog_mklib="\$(CC) ${brltty_mklib_gcc_make=-shared} BRLTTY_OPTIONS_LD2CC([${brltty_mklib_ld_options+${brltty_mklib_ld_options} <name>}]) -o"
else
   brltty_cv_prog_mklib="\$(LD) ${brltty_mklib_ld_make} ${brltty_mklib_ld_options+${brltty_mklib_ld_options} <name>} -o"
fi])
AC_SUBST([MKLIB], ["${brltty_cv_prog_mklib}"])

AC_CACHE_CHECK([for configure shared object directory command], [brltty_cv_prog_conflibdir], [dnl
case "${host_os}"
in
   linux*|gnu*)
      brltty_cv_prog_conflibdir="/sbin/ldconfig -n"
      ;;
   *)
      brltty_cv_prog_conflibdir=":"
      ;;
esac])
AC_SUBST([CONFLIBDIR], ["${brltty_cv_prog_conflibdir}"])

case "${host_os}"
in
   cygwin*|mingw*)
      BRLTTY_HAVE_WINDOWS_LIBRARY([ws2_32])
      BRLTTY_HAVE_WINDOWS_LIBRARY([winmm])
      BRLTTY_HAVE_WINDOWS_LIBRARY([msvcp60])
      ;;
   *)
      AC_SEARCH_LIBS([socket], [socket])
      AC_SEARCH_LIBS([inet_ntoa], [nsl])
      ;;
esac

BRLTTY_ARG_DISABLE(
   [i18n],
   [support for internationalization],
   [],
[dnl
   AC_CHECK_HEADER([libintl.h], [dnl
      AC_HAVE_LIBRARY([intl])
      AC_DEFINE([ENABLE_I18N_SUPPORT], [1],
                [Define this if internationalization support is to be included.])
   ])
])

BRLTTY_ARG_DISABLE(
   [iconv],
   [support for character encoding conversion],
   [],
[dnl
   AC_CHECK_HEADERS([iconv.h], [
      AC_HAVE_LIBRARY([iconv])
   ])
])

BRLTTY_ARG_DISABLE(
   [api],
   [the application programming interface],
   [],
[dnl
   BRLTTY_IF_PTHREADS([dnl
      AC_DEFINE([ENABLE_API], [1],
                [Define this if the application programming interface is to be included.])
      BRLTTY_ARG_PARAMETERS([api], [application programming interface])

      api_objects='api_server.$O rangelist.$O'
      api_libraries="${LIBS}"

      all_api="all-api"
      install_api="install-api"

      api_dynamic_library="api-dynamic-library"
      install_api_libraries="install-api-libraries"
      case "${host_os}"
      in
         cygwin*|mingw*)
            AC_DEFINE([BRLAPI_WIN32], [1],
                      [Define this if the Win32 programming interface is available.])
            api_dynamic_library="api-dynamic-library-windows"
            install_api_libraries="install-api-libraries-windows"
            ;;
      esac
   ])
])
AC_SUBST([api_objects])
AC_SUBST([api_libraries])
AC_SUBST([all_api])
AC_SUBST([install_api])
AC_SUBST([api_dynamic_library])
AC_SUBST([install_api_libraries])
AC_SUBST([api_authfile], [brlapi.key])
BRLTTY_DEFINE_EXPANDED([BRLAPI_AUTHFILE], ["${api_authfile}"],
                       [Define this to be a string containing the name of the file containing the authorization key.])

brltty_curses_libs_save="${LIBS}"
BRLTTY_PACKAGE_CHOOSE([curses],
   [ncursesw ncurses.h],
   [ncurses ncurses.h],
   [curses curses.h])
brltty_libs_curses="${LIBS%${brltty_curses_libs_save}}"
LIBS="${brltty_curses_libs_save}"
AC_SUBST([brltty_libs_curses])

AC_PATH_XTRA
if test "${no_x}" != "yes"
then
   AC_SUBST([all_xbrlapi], [all-xbrlapi])
   AC_SUBST([install_xbrlapi], [install-xbrlapi])

   brltty_x_cppflags_save="${CPPFLAGS}"
   CPPFLAGS="${CPPFLAGS} ${X_CFLAGS}"

   brltty_x_ldflags_save="${LDFLAGS}"
   LDFLAGS="${LDFLAGS} ${X_LIBS}"

   brltty_x_libs_save="${LIBS}"

   AC_HAVE_LIBRARY([X11], [
      LIBS="${X_EXTRA_LIBS} ${X_PRE_LIBS} -lX11 ${LIBS}"

      AC_HAVE_LIBRARY([Xext], [
         LIBS="-lXext ${LIBS}"
         AC_HAVE_LIBRARY([Xtst], [
            LIBS="-lXtst ${LIBS}"
            AC_CHECK_HEADERS([X11/extensions/XTest.h])
            AC_CHECK_HEADERS([X11/extensions/XKB.h])
         ])
      ])

      AC_HAVE_LIBRARY([Xt], [
         LIBS="-lXt ${LIBS}"

         BRLTTY_PACKAGE_CHOOSE([gui_toolkit],
            [Xaw X11/Xaw/Form.h X11/Xaw/Paned.h X11/Xaw/Label.h X11/Xaw/Command.h X11/Xaw/Repeater.h],
            [Xaw3d X11/Xaw3d/Form.h X11/Xaw3d/Paned.h X11/Xaw3d/Label.h X11/Xaw3d/Command.h X11/Xaw3d/Repeater.h],
            [neXtaw X11/neXtaw/Form.h X11/neXtaw/Paned.h X11/neXtaw/Label.h X11/neXtaw/Command.h X11/neXtaw/Repeater.h],
            [XawPlus X11/XawPlus/Form.h X11/XawPlus/Paned.h X11/XawPlus/Label.h X11/XawPlus/Command.h X11/XawPlus/Repeater.h],
            [Xm Xm/Xm.h Xm/Form.h Xm/PanedW.h Xm/Label.h Xm/PushB.h])
      ])
   ])

   brltty_libs_x="${LDFLAGS#${brltty_x_ldflags_save}} ${LIBS%${brltty_x_libs_save}}"
   AC_SUBST([brltty_libs_x])

   LIBS="${brltty_x_libs_save}"
   LDFLAGS="${brltty_x_ldflags_save}"
   CPPFLAGS="${brltty_x_cppflags_save}"
fi

test -z "$brltty_package_gui_toolkit" && {
   case "${host_os}"
   in
      cygwin*|mingw*)
         brltty_package_gui_toolkit="windows"
         ;;
   esac
}

AC_CHECK_HEADERS([alloca.h getopt.h glob.h regex.h syslog.h])
AC_CHECK_HEADERS([sys/poll.h sys/select.h sys/wait.h])
AC_CHECK_HEADERS([pwd.h grp.h])
AC_CHECK_HEADERS([sys/io.h sys/modem.h machine/speaker.h linux/vt.h])

AC_CHECK_HEADERS([linux/input.h], [
   AC_CHECK_HEADERS([linux/uinput.h], [], [], [
#include <linux/input.h>
])])

AC_CHECK_FUNCS([fchdir fchmod getopt_long hstrerror realpath sigaction vsyslog])
AC_CHECK_FUNCS([shmget shm_open])
AC_CHECK_FUNCS([getpeereid getpeerucred getzoneid])

case "${host_os}"
in
   cygwin*|mingw*)
      if test "${brltty_cv_dll_ws2_32}" = "yes"
      then
         BRLTTY_HAVE_WINDOWS_FUNCTION([getaddrinfo], [ws2_32])
         BRLTTY_HAVE_WINDOWS_FUNCTION([getnameinfo], [ws2_32])
         AC_CHECK_FUNCS([gai_strerror])
      fi
      ;;
   *)
      AC_CHECK_FUNCS([gai_strerror getaddrinfo getnameinfo])
      ;;
esac

AC_CHECK_SIZEOF([key_t], [], [
#include <sys/ipc.h>
])

BRLTTY_IF_PTHREADS([dnl
   AC_DEFINE([HAVE_POSIX_THREADS], [1],
             [Define this if posix threads are supported.])
])

AC_C_BIGENDIAN
AC_C_CONST
AC_C_INLINE
AC_OBJEXT
AC_EXEEXT

AC_CACHE_CHECK(
   [if the compiler understands the __alignof__ operator],
   [brltty_cv_c_operator_alignof],
   [AC_TRY_COMPILE([
int a;
int b = __alignof__(a);
],
[],
[brltty_cv_c_operator_alignof=yes],
[brltty_cv_c_operator_alignof=no])])
if test "${brltty_cv_c_operator_alignof}" = "yes"
then
   AC_DEFINE([HAVE_OPERATOR_ALIGNOF],  [1],
             [Define this if the __alignof__ operator is supported.])
fi

AC_CACHE_CHECK(
   [if the compiler understands __attribute__((format(printf)))],
   [brltty_cv_c_attribute_format_printf],
   [AC_TRY_COMPILE([
extern void conf_attribute_format_printf(const char *format, ...)
       __attribute__((format(printf, 1, 2)));
],
[],
[brltty_cv_c_attribute_format_printf=yes],
[brltty_cv_c_attribute_format_printf=no])])
if test "${brltty_cv_c_attribute_format_printf}" = "yes"
then
   AC_DEFINE([HAVE_ATTRIBUTE_FORMAT_PRINTF], [1],
             [Define this if the printf format attribute is supported.])
fi

AC_CACHE_CHECK(
   [if the compiler understands __attribute__((packed))],
   [brltty_cv_c_attribute_packed],
   [AC_TRY_COMPILE([
typedef struct {
  char c;
  int i;
} __attribute__((packed)) conf_attribute_packed;
],
[],
[brltty_cv_c_attribute_packed=yes],
[brltty_cv_c_attribute_packed=no])])
if test "${brltty_cv_c_attribute_packed}" = "yes"
then
   AC_DEFINE([HAVE_ATTRIBUTE_PACKED], [1],
             [Define this if the packed attribute is supported.])
fi

AC_CACHE_CHECK(
   [if the compiler understands __attribute__((unused))],
   [brltty_cv_c_attribute_unused],
   [AC_TRY_COMPILE([
static void conf_attribute_unused (void)
       __attribute__((unused));
],
[],
[brltty_cv_c_attribute_unused=yes],
[brltty_cv_c_attribute_unused=no])])
if test "${brltty_cv_c_attribute_unused}" = "yes"
then
   AC_DEFINE([HAVE_ATTRIBUTE_UNUSED], [1],
             [Define this if the unused attribute is supported.])
fi

AC_CACHE_CHECK([for system-dependent compilation flags], [brltty_cv_prog_cc_sysflags], [dnl
case "${host_os}"
in
   linux*|gnu*)
      brltty_cv_prog_cc_sysflags="-D_POSIX_C_SOURCE=2 -D_BSD_SOURCE -D_XOPEN_SOURCE=500"
      ;;
   solaris*)
      brltty_cv_prog_cc_sysflags="-D_XOPEN_SOURCE=500 -D__EXTENSIONS__"
      ;;
   hpux*)
      brltty_cv_prog_cc_sysflags="-D_XOPEN_SOURCE_EXTENDED -D_HPUX_SOURCE"
      ;;
   osf*)
      brltty_cv_prog_cc_sysflags="-D_XOPEN_SOURCE=500 -D_POSIX_C_SOURCE -D_OSF_SOURCE"
      ;;
   *)
      brltty_cv_prog_cc_sysflags=""
      ;;
esac])
SYSCFLAGS="${brltty_cv_prog_cc_sysflags}"
AC_SUBST([SYSCFLAGS])

AC_CACHE_CHECK([for c compiler yacc source flags], [brltty_cv_prog_cc_yaccflags], [dnl
if test "${GCC}" = "yes"
then
   brltty_cv_prog_cc_yaccflags="-Wno-parentheses -Wno-unused -Wno-uninitialized -Wno-unknown-pragmas"
else
   case "${host_os}"
   in
      *)
         brltty_cv_prog_cc_yaccflags=""
         ;;
   esac
fi])
YCFLAGS="${brltty_cv_prog_cc_yaccflags}"
AC_SUBST([YCFLAGS])

AC_CACHE_CHECK([for c compiler shared object flags], [brltty_cv_prog_cc_libflags], [dnl
if test "${GCC}" = "yes"
then
   case "${host_os}"
   in
      cygwin*|mingw*)
         brltty_cv_prog_cc_libflags=""
         ;;
      msdos*)
         brltty_cv_prog_cc_libflags=""
         ;;
      darwin*)
         brltty_cv_prog_cc_libflags=""
         ;;
      *)
         brltty_cv_prog_cc_libflags="-fPIC"
         ;;
   esac
else
   case "${host_os}"
   in
      *)
         brltty_cv_prog_cc_libflags=""
         AC_MSG_WARN([library flags not configured for ${host_os}])
         ;;
   esac
fi])
AC_SUBST([LIBCFLAGS], ["${brltty_cv_prog_cc_libflags}"])

AC_CACHE_CHECK([for c++ compiler shared object flags], [brltty_cv_prog_cxx_libflags], [dnl
if test "$GXX" = "yes"
then
   brltty_cv_prog_cxx_libflags="-fPIC"
else
   case "${host_os}"
   in
      *)
         brltty_cv_prog_cxx_libflags=""
         ;;
   esac
fi])
LIBCXXFLAGS="${brltty_cv_prog_cxx_libflags}"
AC_SUBST([LIBCXXFLAGS])

AC_SUBST([archive_prefix], [lib])
AC_SUBST([archive_extension], [a])

case "${host_os}"
in
   cygwin*)
      library_prefix="cyg"
      ;;
   mingw*)
      library_prefix=""
      ;;
   *)
      library_prefix="${archive_prefix}"
      ;;
esac
AC_SUBST([library_prefix])

case "${host_os}"
in
   hpux*)
      library_extension="sl"
      ;;
   cygwin*|mingw*)
      library_extension="dll"
      ;;
   darwin*)
      library_extension="dylib"
      ;;
   *)
      library_extension="so"
      ;;
esac
AC_SUBST([library_extension])
AC_DEFINE_UNQUOTED([LIBRARY_EXTENSION], ["${library_extension}"],
                   [Define this to be a string containing the library extension.])

AC_SUBST([module_name], ["${library_prefix}${PACKAGE_NAME}"])
AC_DEFINE_UNQUOTED([MODULE_NAME], ["${module_name}"],
                   [Define this to be a string containing the base module name.])

case "${host_os}"
in
   darwin*)
      module_extension="bundle"
      ;;
   *)
      module_extension="${library_extension}"
      ;;
esac
AC_SUBST([module_extension])
AC_DEFINE_UNQUOTED([MODULE_EXTENSION], ["${module_extension}"],
                   [Define this to be a string containing the module extension.])

AC_CACHE_CHECK([for static archive extension], [brltty_cv_ext_arc], [dnl
case "${host_os}"
in
   *)
      brltty_cv_ext_arc="a"
      ;;
esac])
archive_extension="${brltty_cv_ext_arc}"
AC_SUBST([archive_extension])

AC_CACHE_CHECK([for shared object run-time search path environment variable], [brltty_cv_env_libsearch], [dnl
case "${host_os}"
in
   hpux*)
      brltty_cv_env_libsearch="SHLIB_PATH"
      ;;
   darwin*)
      brltty_cv_env_libsearch="DYLD_LIBRARY_PATH"
      ;;
   *)
      brltty_cv_env_libsearch="LD_LIBRARY_PATH"
      ;;
esac])
libsearch_variable="${brltty_cv_env_libsearch}"
AC_SUBST([libsearch_variable])

brltty_pcm_interfaces=""
brltty_midi_interfaces=""
case "${host_os}"
in
   linux*)
      system_object="linux"
      brltty_pcm_interfaces="oss"
      brltty_midi_interfaces="oss"
      AC_CHECK_HEADER([alsa/asoundlib.h], [dnl
         brltty_pcm_interfaces="${brltty_pcm_interfaces} alsa"
         brltty_midi_interfaces="${brltty_midi_interfaces} alsa"
])
      ;;
   gnu*)
      system_object="hurd"
      ;;
   solaris*)
      system_object="solaris"
      ;;
   hpux*)
      system_object="hpux"
      if test -d /opt/audio/include
      then
         SYSCFLAGS="${SYSCFLAGS} -I/opt/audio/include"
         LIBS="-L/opt/audio/lib -lAlib ${LIBS}"
         AC_DEFINE([HAVE_HPUX_AUDIO], [1],
                   [Define this if HP-UX audio support is available.])
      fi
      ;;
   openbsd*)
      system_object="openbsd"
      ;;
   freebsd*)
      system_object="freebsd"
      ;;
   netbsd*)
      system_object="netbsd"
      ;;
   osf*)
      system_object="osf"
      ;;
   *qnx*)
      system_object="qnx"
      AC_CHECK_LIB([asound], [snd_pcm_open])
      ;;
   cygwin*|mingw*)
      system_object="windows"
      ;;
   msdos*)
      system_object="msdos"
      ;;
   darwin*)
      system_object="darwin"
      LIBS="-lIOKit -framework CoreFoundation ${LIBS}"
      ;;
   *)
      AC_MSG_ERROR([no system object available for ${host_os}])
      ;;
esac
system_object="sys_${system_object}."'$O'
AC_SUBST([system_object])

BRLTTY_ARG_DISABLE(
   [preferences-menu],
   [the preferences menu],
   [],
[dnl
   AC_DEFINE([ENABLE_PREFERENCES_MENU], [1],
             [Define this to include the preferences menu.])
])

BRLTTY_ARG_DISABLE(
   [table-selection],
   [text/attribute/contraction table selection in preferences menu],
   [],
[dnl
   AC_DEFINE([ENABLE_TABLE_SELECTION], [1],
             [Define this to include table selection within the preferences menu.])
])
translation_table_objects='tbl.$O'
AC_SUBST([translation_table_objects])

BRLTTY_ARG_DISABLE(
   [learn-mode],
   [interactive command learn mode],
   [],
[dnl
   AC_DEFINE([ENABLE_LEARN_MODE], [1],
             [Define this to include command learn mode.])
])

BRLTTY_ARG_DISABLE(
   [contracted-braille],
   [in-line contracted braille],
   [],
[dnl
   AC_DEFINE([ENABLE_CONTRACTED_BRAILLE], [1],
             [Define this to include contraction table support.])
   contracted_braille_objects='ctb_compile.$O ctb_translate.$O'
   install_contraction_tables=install-contraction-tables
])
AC_SUBST([contracted_braille_objects])
AC_SUBST([install_contraction_tables])

BRLTTY_ARG_DISABLE(
   [beeper-support],
   [support for the console tone generator],
   [],
[dnl
   AC_DEFINE([ENABLE_BEEPER_SUPPORT], [1],
             [Define this to include beeper (console tone generator) support.])
   beeper_tune_objects='beeper.$O'
])
AC_SUBST([beeper_tune_objects])

BRLTTY_ARG_DISABLE(
   [pcm-support],
   [support for soundcard digital audio],
   [${brltty_pcm_interfaces}],
[dnl
   AC_DEFINE([ENABLE_PCM_SUPPORT], [1],
             [Define this to include PCM (soundcard audio) support.])
   pcm_tune_objects='pcm.$O'
])
AC_SUBST([pcm_tune_objects])

BRLTTY_ARG_DISABLE(
   [midi-support],
   [support for the Musical Instrument Digital Interface],
   [${brltty_midi_interfaces}],
[dnl
   AC_DEFINE([ENABLE_MIDI_SUPPORT], [1],
             [Define this to include MIDI (Musical Instrument Digital Interface) support.])
   midi_tune_objects='midi.$O'
])
AC_SUBST([midi_tune_objects])

BRLTTY_ARG_DISABLE(
   [fm-support],
   [support for the soundcard synthesizer],
   [],
[dnl
   AC_DEFINE([ENABLE_FM_SUPPORT], [1],
             [Define this to include FM (soundcard synthesizer) support.])
   fm_tune_objects='fm.$O adlib.$O'
])
AC_SUBST([fm_tune_objects])

BRLTTY_ARG_DISABLE(
   [pm-configfile],
   [user customization of Papenmeier driver],
   [],
[dnl
   AC_DEFINE([ENABLE_PM_CONFIGURATION_FILE], [1],
             [Define this to include Papenmeier configuration file support.])
   PM_CONFIGFILE_DEPENDENCY=configuration-file
])
AC_SUBST([PM_CONFIGFILE_DEPENDENCY])

BRLTTY_ARG_ENABLE(
   [gpm],
   [mouse tracking via GPM],
   [],
[dnl
   AC_CHECK_LIB([gpm], [Gpm_Open])
])

AC_CACHE_CHECK([for linkage editor program creation flags], [brltty_cv_prog_ld_flags], [dnl
case "${host_os}"
in
   linux*|gnu*|openbsd*|freebsd*|netbsd*|*qnx*)
      brltty_cv_prog_ld_flags="-export-dynamic"
      ;;
   solaris*)
      brltty_cv_prog_ld_flags=""
      ;;
   hpux*)
      brltty_cv_prog_ld_flags="-E +s"
      ;;
   osf*)
      brltty_cv_prog_ld_flags=""
      ;;
   cygwin*|mingw*)
      brltty_cv_prog_ld_flags="-export-dynamic --enable-auto-import"
      ;;
   msdos*)
      brltty_cv_prog_ld_flags=""
      ;;
   darwin*)
      brltty_cv_prog_ld_flags=""
      ;;
   *)
      AC_MSG_ERROR([linkage editor program creation flags not configured for ${host_os}])
      ;;
esac])
brltty_link_flags="${brltty_cv_prog_ld_flags}"

AC_CACHE_CHECK([for static linkage flag], [brltty_cv_prog_ld_static], [dnl
if test "${GCC}" = "yes"
then
   brltty_cv_prog_ld_static="-static"
else
   case "${host_os}"
   in
      linux*|gnu*|openbsd*|freebsd*|*qnx*|cygwin*|mingw*)
         brltty_cv_prog_ld_static="-static"
         ;;
      netbsd*)
         brltty_cv_prog_ld_static="-Bstatic"
         ;;
      solaris*)
         brltty_cv_prog_ld_static="-dn"
         ;;
      hpux*)
         brltty_cv_prog_ld_static="-Bstatic"
         ;;
      osf*)
         brltty_cv_prog_ld_static="-non_shared"
         ;;
      *)
         AC_MSG_ERROR([static linkage not supported by ${host_os}])
         ;;
   esac
   brltty_cv_prog_ld_static="BRLTTY_OPTIONS_LD2CC([${brltty_cv_prog_ld_static}])"
fi])
brltty_static_flag="${brltty_cv_prog_ld_static}"
BRLTTY_ARG_ENABLE(
   [standalone-programs],
   [statically linked executables],
   [],
[dnl
   brltty_standalone_programs="yes"
   LDFLAGS="${LDFLAGS} ${brltty_static_flag}"
])
LDFLAGS="${LDFLAGS} BRLTTY_OPTIONS_LD2CC([${brltty_link_flags}])"

case "${host_os}"
in
   linux*|gnu*|solaris*|openbsd*|freebsd*|netbsd*|osf*|*qnx*|cygwin*|mingw*)
      BRLTTY_SEARCH_LIBS([dlopen], [dl])
      ;;
   hpux*)
      BRLTTY_SEARCH_LIBS([shl_load], [dld])
      ;;
   *)
      ;;
esac

case "${host_os}"
in
   linux*)
      usb_object="linux"
      ;;
   solaris*)
      usb_object="solaris"
      AC_HAVE_LIBRARY([aio])
      AC_HAVE_LIBRARY([rt])
      ;;
   openbsd*)
      usb_object="openbsd"
      ;;
   freebsd*)
      usb_object="freebsd"
      ;;
   netbsd*)
      usb_object="netbsd"
      ;;
   darwin*)
      usb_object="darwin"
      ;;
   *)
      AC_CHECK_HEADERS([usb.h], [usb_object="libusb"])
      ;;
esac
if test -n "${usb_object}"
then
   BRLTTY_ARG_DISABLE(
      [usb-support],
      [support for the Universal Serial Bus],
      [],
[dnl
      AC_DEFINE([ENABLE_USB_SUPPORT], [1],
                [Define this if Universal Serial Bus support is to be included.])
      case "${usb_object}"
      in
         libusb)
            AC_HAVE_LIBRARY([usb])
            ;;
      esac
      usb_objects="usb."'$O'" usb_${usb_object}."'$O'
])
else
   AC_MSG_WARN([no USB support on ${host_os}])
fi
AC_SUBST([usb_objects])

case "${host_os}"
in
   linux*)
      bluetooth_object="linux"
      ;;
   *)
      bluetooth_object=""
      AC_MSG_WARN([no bluetooth support on ${host_os}])
      ;;
esac
if test -n "${bluetooth_object}"
then
   AC_CHECK_HEADER([bluetooth/bluetooth.h], [dnl
      BRLTTY_ARG_DISABLE(
         [bluetooth-support],
         [support for bluetooth capable devices],
         [],
[dnl
         AC_DEFINE([ENABLE_BLUETOOTH_SUPPORT], [1],
                   [Define this if bluetooth support is to be included.])
         bluetooth_objects="bluetooth."'$O'" bluetooth_${bluetooth_object}."'$O'
])
])
fi
AC_SUBST([bluetooth_objects])

BRLTTY_BRAILLE_DRIVER([al], [Alva])
BRLTTY_BRAILLE_DRIVER([at], [Albatross])
test -n "${api_objects}" && {
   BRLTTY_BRAILLE_DRIVER([ba], [BrlAPI], [-L$(BLD_TOP)$(PGM_DIR) -lbrlapi])
}
BRLTTY_BRAILLE_DRIVER([bd], [Braudi])
BRLTTY_BRAILLE_DRIVER([bl], [BrailleLite])
BRLTTY_BRAILLE_DRIVER([bm], [Baum])
BRLTTY_BRAILLE_DRIVER([bn], [BrailleNote])
BRLTTY_BRAILLE_DRIVER([cb], [CombiBraille])
BRLTTY_BRAILLE_DRIVER([ec], [EcoBraille])
BRLTTY_BRAILLE_DRIVER([eu], [EuroBraille])
BRLTTY_BRAILLE_DRIVER([fs], [FreedomScientific])
BRLTTY_BRAILLE_DRIVER([ht], [HandyTech])
case "${host_os}"
in
   linux*)
      BRLTTY_BRAILLE_DRIVER([il], [IrisLinux])
      ;;
esac
BRLTTY_IF_PACKAGE([Libbraille], [libbraille], [include/braille.h], [dnl
   BRLTTY_BRAILLE_DRIVER([lb], [Libbraille], [-L$(LIBBRAILLE_ROOT)/lib -lbraille])
])
BRLTTY_BRAILLE_DRIVER([lt], [LogText])
BRLTTY_BRAILLE_DRIVER([mb], [MultiBraille])
BRLTTY_BRAILLE_DRIVER([md], [MDV])
BRLTTY_BRAILLE_DRIVER([mn], [MiniBraille])
BRLTTY_BRAILLE_DRIVER([pm], [Papenmeier])
BRLTTY_BRAILLE_DRIVER([tn], [TechniBraille])
BRLTTY_BRAILLE_DRIVER([ts], [TSI])
BRLTTY_BRAILLE_DRIVER([tt], [TTY], [$(CLIBS)])
BRLTTY_BRAILLE_DRIVER([vd], [VideoBraille])
BRLTTY_BRAILLE_DRIVER([vo], [Voyager])
BRLTTY_BRAILLE_DRIVER([vr], [Virtual])
BRLTTY_BRAILLE_DRIVER([vs], [VisioBraille])
test -n "${brltty_package_gui_toolkit}" && {
   BRLTTY_BRAILLE_DRIVER([xw], [XWindow], [$(XLIBS)])
}
BRLTTY_ARG_DRIVER([braille], [BrailleDrivers])

AC_CACHE_CHECK([for device directory], [brltty_cv_device_directory], [dnl
case "${host_os}"
in
   cygwin*|mingw*)
      brltty_cv_device_directory=""
      ;;
   *)
      brltty_cv_device_directory="/dev"
      ;;
esac])
AC_DEFINE_UNQUOTED([DEVICE_DIRECTORY], ["${brltty_cv_device_directory}"],
                   [Define this to be a string containing the path to the directory containing the devices.])

AC_CACHE_CHECK([for first serial device], [brltty_cv_device_serial_first], [dnl
case "${host_os}"
in
   linux*)
      for brltty_device in "ttyS0" "tts/0"
      do
         test -c "${brltty_cv_device_directory}/${brltty_device}" && {
            brltty_cv_device_serial_first="${brltty_device}"
            break
         }
      done
      ;;
   gnu*)
      brltty_cv_device_serial_first="com0"
      ;;
   solaris*)
      brltty_cv_device_serial_first="ttya"
      ;;
   hpux*)
      brltty_cv_device_serial_first="tty0p0"
      ;;
   openbsd*)
      brltty_cv_device_serial_first="cua00"
      ;;
   freebsd*)
      brltty_cv_device_serial_first="cuaa0"
      ;;
   netbsd*)
      brltty_cv_device_serial_first="dty00"
      ;;
   osf*)
      brltty_cv_device_serial_first="tty00"
      ;;
   *qnx*)
      brltty_cv_device_serial_first="ser1"
      ;;
   cygwin*|mingw*|msdos*)
      brltty_cv_device_serial_first="COM1"
      ;;
   *)
      brltty_cv_device_serial_first=""
      AC_MSG_WARN([no default braille device])
      ;;
esac])
AC_SUBST([first_serial_device], [${brltty_cv_device_serial_first}])

BRLTTY_ARG_WITH(
   [braille-device], [DEVICE],
   [default braille display device],
   [braille_device], ["yes"]
)
if test "${braille_device}" = "no"
then
   braille_device=""
elif test "${braille_device}" = "yes"
then
   if test -n "${usb_objects}"
   then
      braille_device="usb:"
   else
      braille_device="serial:${first_serial_device}"
   fi
fi
AC_SUBST([braille_device])
AC_DEFINE_UNQUOTED([BRAILLE_DEVICE], ["${braille_device}"],
                   [Define this to be a string containing the path to the default braille device.])
BRLTTY_SUMMARY_ITEM([braille-device], [braille_device])

BRLTTY_TEXT_TABLE([cz], [Czech (iso-8859-2)])
BRLTTY_TEXT_TABLE([da], [Danish (iso-8859-1)])
BRLTTY_TEXT_TABLE([de], [German (iso-8859-1)])
BRLTTY_TEXT_TABLE([en_UK], [English (United Kingdom)])
BRLTTY_TEXT_TABLE([en_US], [English (United States)])
BRLTTY_TEXT_TABLE([es], [Spanish (iso-8859-1)])
BRLTTY_TEXT_TABLE([fi1], [Finnish (iso-8859-1)])
BRLTTY_TEXT_TABLE([fi2], [Finnish (iso-8859-1)])
BRLTTY_TEXT_TABLE([fr_CA], [French (Canada)])
BRLTTY_TEXT_TABLE([fr_FR], [French (France)])
BRLTTY_TEXT_TABLE([fr-cbifs], [Code Braille Informatique Français Standard (iso-8859-1)])
BRLTTY_TEXT_TABLE([it], [Italian (iso-8859-1)])
BRLTTY_TEXT_TABLE([nabcc], [North American Braille Computer Code (iso-8859-1)])
BRLTTY_TEXT_TABLE([no-h], [Norwegian and German (iso-8859-1)])
BRLTTY_TEXT_TABLE([no-p], [Norwegian (iso-8859-1)])
BRLTTY_TEXT_TABLE([pl], [Polish (iso-8859-2)])
BRLTTY_TEXT_TABLE([ru], [Russian (koi8-r)])
BRLTTY_TEXT_TABLE([se], [Swedish (1996 standard)])
BRLTTY_TEXT_TABLE([se-old], [Swedish (1989 standard)])
BRLTTY_TEXT_TABLE([vni], [Vietnamese (iso-8859-1)])
BRLTTY_ARG_TABLE([text], [nabcc], [text])

BRLTTY_ATTRIBUTES_TABLE([attributes], [])
BRLTTY_ATTRIBUTES_TABLE([attrib], [])
BRLTTY_ARG_TABLE([attributes], [attributes], [])

BRLTTY_ARG_DISABLE(
   [speech-support],
   [support for speech synthesizers],
   [],
[dnl
   AC_DEFINE([ENABLE_SPEECH_SUPPORT], [1],
             [Define this to include speech synthesizer support.])
   speech_support_object='spk.$O'

   BRLTTY_SPEECH_DRIVER([al], [Alva])
   BRLTTY_SPEECH_DRIVER([bl], [BrailleLite])
   BRLTTY_SPEECH_DRIVER([cb], [CombiBraille])
   BRLTTY_SPEECH_DRIVER([es], [ExternalSpeech])
   BRLTTY_IF_PACKAGE([FestivalLite], [flite], [include/flite/flite.h], [dnl
      BRLTTY_ARG_REQUIRED(
         [flite-language], [LANGUAGE],
         [the FestivalLite language to use],
         [flite_language], ["usenglish"]
      )

      BRLTTY_ARG_REQUIRED(
         [flite-lexicon], [LEXICON],
         [the FestivalLite lexicon to use],
         [flite_lexicon], ["cmulex"]
      )

      BRLTTY_ARG_REQUIRED(
         [flite-voice], [VOICE],
         [the FestivalLite voice to use],
         [flite_voice], ["cmu_us_kal16"]
      )

      BRLTTY_SPEECH_DRIVER([fl], [FestivalLite], [-L$(FLITE_ROOT)/lib -lflite_$(FLITE_VOICE) -lflite_$(FLITE_LEXICON) -lflite_$(FLITE_LANGUAGE) -lflite -lm])
   ])
   BRLTTY_SPEECH_DRIVER([fv], [Festival])
   BRLTTY_SPEECH_DRIVER([gs], [GenericSay])
   BRLTTY_IF_PACKAGE([Mikropuhe], [mikropuhe], [mpwrfile.h], [BRLTTY_IF_PTHREADS([BRLTTY_SPEECH_DRIVER([mp], [Mikropuhe], [-L$(MPLINUX_ROOT) -lmplinux -lm])])])
   BRLTTY_IF_PACKAGE([Swift], [swift], [include/swift.h], [BRLTTY_SPEECH_DRIVER([sw], [Swift], [-L$(SWIFT_ROOT)/lib -lswift -lm])])
   BRLTTY_IF_PACKAGE([Theta], [theta], [include/theta.h], [BRLTTY_SPEECH_DRIVER([th], [Theta], [-L$(THETA_ROOT)/lib -ltheta])])
   BRLTTY_IF_PACKAGE([ViaVoice], [viavoice], [include/eci.h], [BRLTTY_SPEECH_DRIVER([vv], [ViaVoice], [-L$(VIAVOICE_ROOT)/lib -libmeci50])])
])
AC_SUBST([speech_support_object])
BRLTTY_ARG_DRIVER([speech], [SpeechDrivers])

case "${host_os}"
in
   linux*)
      BRLTTY_SCREEN_DRIVER([lx], [Linux])
      ;;
   gnu*)
      BRLTTY_SCREEN_DRIVER([hd], [Hurd])
      ;;
   cygwin*|mingw*)
      BRLTTY_SCREEN_DRIVER([wn], [Windows])
      ;;
   msdos*)
      BRLTTY_SCREEN_DRIVER([pb], [PCBIOS])
      ;;
   *)
      AC_MSG_WARN([no native screen driver for ${host_os}])
      ;;
esac
BRLTTY_SCREEN_DRIVER([sc], [Screen])

cspi_includes=""
cspi_libdirs=""
cspi_libraries=""
for package in cspi-1.0
do
   pkg-config --exists "${package}" && {
      cspi_includes="`pkg-config --cflags-only-I "${package}"`"
      cspi_libdirs="`pkg-config --libs-only-L "${package}"`"
      cspi_libraries="`pkg-config --libs-only-l "${package}"`"
      BRLTTY_SCREEN_DRIVER([as], [AtSpi], [$(CSPI_LIBDIRS) $(CSPI_LIBRARIES)])
      break
   }
done
AC_SUBST([cspi_includes])
AC_SUBST([cspi_libdirs])
AC_SUBST([cspi_libraries])

BRLTTY_ARG_DRIVER([screen], [ScreenDrivers])
AC_DEFINE_UNQUOTED([SCREEN_DRIVER], ["${brltty_default_code_screen}"],
                   [Define this to be a string containing the default screen driver code.])
BRLTTY_SUMMARY_ITEM([screen-driver], [brltty_default_code_screen])

AC_SUBST([install_drivers])
BRLTTY_SUMMARY_END

changequote(, )dnl
brltty_make_files="`echo "${brltty_build_directories}" | sed -e '
s%\([^ ][^ ]*\)%\1/Makefile:prologue.mk.in:\1/Makefile.in%g
'`"
changequote([, ])dnl

AC_OUTPUT([
   config.mk
   brltty.spec
   brltty.lsm
   Programs/brltty-config
   Documents/brltty.conf
   Documents/brltty.1
   Documents/BrlAPIref.doxy
   BrailleDrivers/Voyager/mkhlp
   ${brltty_make_files}])
