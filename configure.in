dnl Process this file with autoconf 2.53 to produce a configure script.
###############################################################################
# BRLTTY - A background process providing access to the console screen (when in
#          text mode) for a blind person using a refreshable braille display.
#
# Copyright (C) 1995-2005 by The BRLTTY Team. All rights reserved.
#
# BRLTTY comes with ABSOLUTELY NO WARRANTY.
#
# This is free software, placed under the terms of the
# GNU General Public License, as published by the Free Software
# Foundation.  Please see the file COPYING for details.
#
# Web Page: http://mielke.cc/brltty/
#
# This software is maintained by Dave Mielke <dave@mielke.cc>.
###############################################################################

AC_PREREQ([2.53])
AC_INIT([brltty], [3.7pre1], [http://mielke.cc/brltty/])
AC_CONFIG_SRCDIR([Programs/brltty.h])
BRLTTY_SUMMARY_BEGIN
brltty_build_directories=". Programs Documents"

AC_SUBST([api_major], [0])
AC_SUBST([api_minor], [4])
AC_SUBST([api_revision], [0])
AC_SUBST([api_version], [${api_major}.${api_minor}])
AC_SUBST([api_release], [${api_version}.${api_revision}])
AC_DEFINE_UNQUOTED([BRLAPI_RELEASE], ["${api_release}"])

AC_SUBST([PACKAGE_TITLE], ["`echo "${PACKAGE_NAME}" | sed -e 'y%abcdefghijklmnopqrstuvwxyz%ABCDEFGHIJKLMNOPQRSTUVWXYZ%'`"])
AC_DEFINE_UNQUOTED([PACKAGE_TITLE], ["${PACKAGE_TITLE}"])

AC_SUBST([package_date], ["`date "+%Y/%m/%d"`"])

AC_CONFIG_HEADER([config.h])
AC_CONFIG_HEADER([Programs/api.h])
AC_PREFIX_DEFAULT([])
AC_CONFIG_AUX_DIR([acdir])
AC_CANONICAL_SYSTEM

brltty_default_execute_root="/${PACKAGE_NAME}-${PACKAGE_VERSION}"
BRLTTY_ARG_WITH(
   [execute-root], [DIRECTORY],
   [where the package is to be anchored at run-time],
   [execute_root], [""]
)
if test "${execute_root}" = "no"
then
   execute_root=""
elif test "${execute_root}" = "yes"
then
   execute_root="${brltty_default_execute_root}"
fi
AC_SUBST([execute_root])
BRLTTY_SUMMARY_ITEM([execute-root], [execute_root])

brltty_default_install_root="/tmp/${PACKAGE_NAME}-${PACKAGE_VERSION}"
BRLTTY_ARG_WITH(
   [install-root], [DIRECTORY],
   [where to install the package],
   [install_root], ["${execute_root}"]
)
if test "${install_root}" = "no"
then
   install_root="${execute_root}"
elif test "${install_root}" = "yes"
then
   install_root="${brltty_default_install_root}"
fi
AC_SUBST([install_root])
BRLTTY_SUMMARY_ITEM([install-root], [install_root])

AC_SUBST([configuration_file], ["${PACKAGE_NAME}.conf"])
BRLTTY_DEFINE_EXPANDED([CONFIGURATION_FILE], ["${configuration_file}"])

BRLTTY_PORTABLE_DIRECTORY([includedir], [/usr])
BRLTTY_PORTABLE_DIRECTORY([mandir], [/usr/share])

BRLTTY_SUMMARY_ITEM([libdir], [libdir])
BRLTTY_SUMMARY_ITEM([sysconfdir], [sysconfdir])

BRLTTY_ARG_REQUIRED(
   [program-directory], [DIRECTORY],
   [path to directory containing executables],
   [program_directory], ["${bindir}"]
)

BRLTTY_ARG_REQUIRED(
   [library-directory], [DIRECTORY],
   [path to directory containing drivers],
   [library_directory], ["${libdir}/${PACKAGE_NAME}"]
)

BRLTTY_ARG_REQUIRED(
   [data-directory], [DIRECTORY],
   [path to directory containing tables and help pages],
   [data_directory], ["${sysconfdir}/${PACKAGE_NAME}"]
)

BRLTTY_ARG_REQUIRED(
   [manpage-directory], [DIRECTORY],
   [path to directory containing manual pages],
   [manpage_directory], ["${mandir}"]
)

BRLTTY_ARG_REQUIRED(
   [include-directory], [DIRECTORY],
   [path to directory containing header files],
   [include_directory], ["${includedir}/${PACKAGE_NAME}"]
)

original_prefix="${prefix}"
test "${prefix}" = "NONE" && prefix=""
original_exec_prefix="${exec_prefix}"
test "${exec_prefix}" = "NONE" && exec_prefix="${prefix}"
BRLTTY_DEFINE_DIRECTORY([DATA_DIRECTORY], [${execute_root}${data_directory}])
BRLTTY_DEFINE_DIRECTORY([LIBRARY_DIRECTORY], [${execute_root}${library_directory}])
BRLTTY_DEFINE_DIRECTORY([CONFIGURATION_DIRECTORY], [${sysconfdir}])
BRLTTY_DEFINE_EXPANDED([BRLAPI_ETCDIR], ["${CONFIGURATION_DIRECTORY}"])
prefix="${original_prefix}"
exec_prefix="${original_exec_prefix}"

brltty_default_compiler_prefix="${host_os}-"
BRLTTY_ARG_WITH(
   [compiler-prefix], [PREFIX],
   [prefix for compiler programs],
   [compiler_prefix], [""]
)
if test "${compiler_prefix}" = "no"
then
   compiler_prefix=""
elif test "${compiler_prefix}" = "yes"
then
   compiler_prefix="${brltty_default_compiler_prefix}"
fi
AC_SUBST([compiler_prefix])
BRLTTY_SUMMARY_ITEM([compiler-prefix], [compiler_prefix])

brltty_default_init_path=/sbin/init_real
BRLTTY_ARG_WITH(
   [init-path], [FILE],
   [path to real init program],
   [init_path], [""]
)
if test "${init_path}" = "no"
then
   init_path=""
elif test "${init_path}" = "yes"
then
   init_path="${brltty_default_init_path}"
fi
if test -n "${init_path}"
then
   AC_DEFINE_UNQUOTED([INIT_PATH], ["${init_path}"])
fi
BRLTTY_SUMMARY_ITEM([init-path], [init_path])

AC_PROG_MAKE_SET

AC_PROG_CC
if test -z "${CXX}"
then
   if test "${GCC}" = "yes"
   then
      AC_SEARCH_LIBS([__cxa_pure_virtual], [supc++])
      AC_CACHE_CHECK([if the C compiler can compile C++], [brltty_cv_prog_cc_cxx], [dnl
      brltty_save_ac_ext="${ac_ext}"
      ac_ext="cc"
      AC_TRY_LINK([], [class c {};],
                  [brltty_cv_prog_cc_cxx="yes"],
                  [brltty_cv_prog_cc_cxx="no"])
      ac_ext="${brltty_save_ac_ext}"])
      test "${brltty_cv_prog_cc_cxx}" = "yes" && CXX="${CC}"
   fi
fi
AC_PROG_CXX

brltty_gcc_options="-Wall"
case "${build_os} ${host_os}"
in
   cygwin*\ mingw*)
      brltty_gcc_options="${brltty_gcc_options} -mno-cygwin"
      ;;
esac
if test "${GCC}" = "yes"
then
   CC="${CC} ${brltty_gcc_options}"
fi
if test "$GXX" = "yes"
then
   CXX="${CXX} ${brltty_gcc_options}"
fi

AC_CHECK_PROGS([LD], [ld], [ld])
AC_PROG_RANLIB
AC_PROG_YACC
AC_PROG_AWK
AC_PROG_LN_S

AC_PROG_INSTALL
BRLTTY_EXECUTABLE_PATH([INSTALL])

AC_CACHE_CHECK([for make relocatable object command], [brltty_cv_prog_mkobj], [dnl
case "${host_os}"
in
   *)
      brltty_cv_prog_mkobj="\$(LD) -r -o"
      ;;
esac])
MKOBJ="${brltty_cv_prog_mkobj}"
AC_SUBST([MKOBJ])

AC_CACHE_CHECK([for loadable module creation command], [brltty_cv_prog_mkmod], [dnl
case "${host_os}"
in
   linux*|gnu*|openbsd*|freebsd*|netbsd*|*qnx*|cygwin*|mingw*)
      brltty_mkmod_ld_make="-shared"
      ;;
   solaris*)
      brltty_mkmod_ld_make="-G"
      ;;
   hpux*)
      brltty_mkmod_ld_make="-b"
      ;;
   osf*)
      brltty_mkmod_ld_make="-shared"
      ;;
   darwin*)
      brltty_mkmod_ld_make="-bundle"
      brltty_mkmod_ld_options="-flat_namespace -undefined suppress"
      brltty_mkmod_gcc_make="-bundle"
      ;;
   *)
      AC_MSG_ERROR([loadable module creation command not configured for ${host_os}])
      ;;
esac
if test "${GCC}" = "yes"
then
   brltty_cv_prog_mkmod="\$(CC) ${brltty_mkmod_gcc_make=-shared} BRLTTY_OPTIONS_LD2CC([${brltty_mkmod_ld_options}]) -o"
else
   brltty_cv_prog_mkmod="\$(LD) ${brltty_mkmod_ld_make} ${brltty_mkmod_ld_options} -o"
fi])
AC_SUBST([MKMOD], ["${brltty_cv_prog_mkmod}"])

AC_CACHE_CHECK([for dynamic library creation command], [brltty_cv_prog_mklib], [dnl
case "${host_os}"
in
   linux*|gnu*|openbsd*|freebsd*|netbsd*|*qnx*|cygwin*|mingw*)
      brltty_mklib_ld_make="-shared"
      brltty_mklib_ld_options="-soname"
      ;;
   solaris*)
      brltty_mklib_ld_make="-G"
      brltty_mklib_ld_options="-h"
      ;;
   hpux*)
      brltty_mklib_ld_make="-b"
      brltty_mklib_ld_options="+h"
      ;;
   osf*)
      brltty_mklib_ld_make="-shared"
      brltty_mklib_ld_options="-expect_unresolved '*' -soname"
      ;;
   darwin*)
      brltty_mklib_ld_make="-dylib"
      brltty_mklib_ld_options="-single_module -install_name"
      brltty_mklib_gcc_make="-dynamiclib"
      ;;
   *)
      AC_MSG_ERROR([dynamic library creation command not configured for ${host_os}])
      ;;
esac
if test "${GCC}" = "yes"
then
   brltty_cv_prog_mklib="\$(CC) ${brltty_mklib_gcc_make=-shared} BRLTTY_OPTIONS_LD2CC([${brltty_mklib_ld_options+${brltty_mklib_ld_options} <name>}]) -o"
else
   brltty_cv_prog_mklib="\$(LD) ${brltty_mklib_ld_make} ${brltty_mklib_ld_options+${brltty_mklib_ld_options} <name>} -o"
fi])
AC_SUBST([MKLIB], ["${brltty_cv_prog_mklib}"])

AC_CACHE_CHECK([for configure shared object directory command], [brltty_cv_prog_conflibdir], [dnl
case "${host_os}"
in
   linux*|gnu*)
      brltty_cv_prog_conflibdir="/sbin/ldconfig -n"
      ;;
   *)
      brltty_cv_prog_conflibdir=":"
      ;;
esac])
AC_SUBST([CONFLIBDIR], ["${brltty_cv_prog_conflibdir}"])

case "${host_os}"
in
   cygwin*|mingw*)
      BRLTTY_HAVE_DLL([wsock32])
      BRLTTY_HAVE_DLL([msvcp60])

      AC_CACHE_CHECK(
         [if the function CreateNamedPipe exists and works],
         [brltty_cv_function_CreateNamedPipe],
         [AC_TRY_RUN([
#include <windows.h>
#include <stdio.h>

int
main (void) {
  DWORD ID = GetCurrentProcessId();
  char path[32];
  HANDLE res;

  snprintf(path, sizeof(path), "\\\\\\\\.\\\\pipe\\\\configure-%ld", ID);
  res = CreateNamedPipe(path, PIPE_ACCESS_DUPLEX,
                        PIPE_TYPE_BYTE | PIPE_READMODE_BYTE,
                        PIPE_UNLIMITED_INSTANCES, 0, 0, 0, NULL);
  return (res == INVALID_HANDLE_VALUE)? 1: 0;
}
],
         [brltty_cv_function_CreateNamedPipe=yes],
         [brltty_cv_function_CreateNamedPipe=no])])
      if test "${brltty_cv_function_CreateNamedPipe}" = "yes"
      then
         AC_DEFINE([HAVE_FUNC_CREATENAMEDPIPE])
      fi

      AC_CACHE_CHECK(
         [if the function GetConsoleWindow exists and works],
         [brltty_cv_function_GetConsoleWindow],
         [AC_TRY_RUN([
#include <windows.h>
#include <stdio.h>
#include <errno.h>

int
main (void) {
  HMODULE module;
  if (!(module = GetModuleHandle("kernel32.dll"))) return 1;
  if (!(GetProcAddress(module, "GetConsoleWindow"))) return 2;
  return 0;
}
],
         [brltty_cv_function_GetConsoleWindow=yes],
         [brltty_cv_function_GetConsoleWindow=no])])
      if test "${brltty_cv_function_GetConsoleWindow}" = "yes"
      then
         AC_DEFINE([HAVE_FUNC_GETCONSOLEWINDOW])
      fi

      AC_CACHE_CHECK(
         [if the function ReadConsoleOutputCharacterW exists and works],
         [brltty_cv_function_ReadConsoleOutputCharacterW],
         [AC_TRY_RUN([
#include <windows.h>

int
main(void) {
  wchar_t buf;
  COORD coord = {0, 0};
  DWORD read;
  HANDLE h = GetStdHandle(STD_OUTPUT_HANDLE);
  if (!ReadConsoleOutputCharacterW(h, &buf, 1, coord, &read)) return 1;
  if (read != 1) return 2;
  return 0;
}
],
         [brltty_cv_function_ReadConsoleOutputCharacterW=yes],
         [brltty_cv_function_ReadConsoleOutputCharacterW=no])])
      if test "${brltty_cv_function_ReadConsoleOutputCharacterW}" = "yes"
      then
         AC_DEFINE([HAVE_FUNC_READCONSOLEOUTPUTCHARACTERW])
      fi

      if test "${build_os}" = "${host_os}"
      then
         brltty_uname_system="`uname -s`"
         set -- `echo "${brltty_uname_system}" | sed -e 's/^.*-//' -e 's/\\./ /g'`
         brltty_windows_version="`printf 0X%02d%02d $1 $2`"
         CFLAGS="${CFLAGS} -DWINVER=${brltty_windows_version}"
      fi
      ;;
   *)
      AC_SEARCH_LIBS([socket], [socket])
      ;;
esac

BRLTTY_ARG_DISABLE(
   [api],
   [the application programming interface],
   [],
[dnl
   BRLTTY_CAP_PTHREADS([dnl
      AC_DEFINE([ENABLE_API])
      api_objects='api_server.$O rangelist.$O'
      all_api="all-api"
      install_api="install-api"
      api_libraries="${LIBS}"
      BRLTTY_ARG_PARAMETERS([api])
   ])
])
AC_SUBST([api_objects])
AC_SUBST([all_api])
AC_SUBST([install_api])
AC_SUBST([api_authfile], [brlapi.key])
AC_SUBST([api_libraries])
BRLTTY_DEFINE_EXPANDED([BRLAPI_AUTHFILE], ["${api_authfile}"])

BRLTTY_PACKAGE_CHOOSE([curses],
   [ncursesw ncurses.h],
   [ncurses ncurses.h],
   [curses curses.h])

AC_PATH_XTRA
if test "${no_x}" != "yes"
then
   LDFLAGS="${LDFLAGS} ${X_LIBS}"

   AC_HAVE_LIBRARY([X11], [
      LIBS="${X_EXTRA_LIBS} ${X_PRE_LIBS} -lX11 ${LIBS}"

      AC_HAVE_LIBRARY([Xt], [
         LIBS="-lXt ${LIBS}"

         BRLTTY_PACKAGE_CHOOSE([gui_toolkit],
            [Xaw X11/Xaw/Form.h X11/Xaw/Paned.h X11/Xaw/Label.h X11/Xaw/Command.h X11/Xaw/Repeater.h],
            [Xaw3d X11/Xaw3d/Form.h X11/Xaw3d/Paned.h X11/Xaw3d/Label.h X11/Xaw3d/Command.h X11/Xaw3d/Repeater.h],
            [neXtaw X11/neXtaw/Form.h X11/neXtaw/Paned.h X11/neXtaw/Label.h X11/neXtaw/Command.h X11/neXtaw/Repeater.h],
            [XawPlus X11/XawPlus/Form.h X11/XawPlus/Paned.h X11/XawPlus/Label.h X11/XawPlus/Command.h X11/XawPlus/Repeater.h],
            [Xm Xm/Xm.h Xm/Form.h Xm/PanedW.h Xm/Label.h Xm/PushB.h])
      ])
   ])
fi

test -z "$brltty_package_gui_toolkit" && {
   case "${host_os}"
   in
      cygwin*|mingw*)
         brltty_package_gui_toolkit="windows"
         ;;
   esac
}

AC_CHECK_HEADERS([alloca.h getopt.h glob.h sys/select.h sys/wait.h syslog.h])
AC_CHECK_HEADERS([pwd.h grp.h])
AC_CHECK_HEADERS([sys/io.h sys/modem.h machine/speaker.h linux/vt.h])

AC_CHECK_HEADERS([linux/input.h], [
   AC_CHECK_HEADERS([linux/uinput.h], [], [], [
#include <linux/input.h>
])])

AC_CHECK_HEADERS([iconv.h], [
   AC_HAVE_LIBRARY([iconv])
])

AC_CHECK_FUNCS([fchdir fchmod gai_strerror getaddrinfo getopt_long hstrerror realpath shmget shm_open sigaction vsyslog])

AC_C_BIGENDIAN
AC_C_CONST
AC_C_INLINE
AC_OBJEXT
AC_EXEEXT

AC_CACHE_CHECK(
   [if the compiler understands the __alignof__ operator],
   [brltty_cv_c_operator_alignof],
   [AC_TRY_COMPILE([
int a;
int b = __alignof__(a);
],
[],
[brltty_cv_c_operator_alignof=yes],
[brltty_cv_c_operator_alignof=no])])
if test "${brltty_cv_c_operator_alignof}" = "yes"
then
   AC_DEFINE([HAVE_OPERATOR_ALIGNOF])
fi

AC_CACHE_CHECK(
   [if the compiler understands __attribute__((format(printf)))],
   [brltty_cv_c_attribute_format_printf],
   [AC_TRY_COMPILE([
extern void conf_attribute_format_printf(const char *format, ...)
       __attribute__((format(printf, 1, 2)));
],
[],
[brltty_cv_c_attribute_format_printf=yes],
[brltty_cv_c_attribute_format_printf=no])])
if test "${brltty_cv_c_attribute_format_printf}" = "yes"
then
   AC_DEFINE([HAVE_ATTRIBUTE_FORMAT_PRINTF])
fi

AC_CACHE_CHECK(
   [if the compiler understands __attribute__((packed))],
   [brltty_cv_c_attribute_packed],
   [AC_TRY_COMPILE([
typedef struct {
  char c;
  int i;
} __attribute__((packed)) conf_attribute_packed;
],
[],
[brltty_cv_c_attribute_packed=yes],
[brltty_cv_c_attribute_packed=no])])
if test "${brltty_cv_c_attribute_packed}" = "yes"
then
   AC_DEFINE([HAVE_ATTRIBUTE_PACKED])
fi

AC_CACHE_CHECK(
   [if the compiler understands __attribute__((unused))],
   [brltty_cv_c_attribute_unused],
   [AC_TRY_COMPILE([
static void conf_attribute_unused (void)
       __attribute__((unused));
],
[],
[brltty_cv_c_attribute_unused=yes],
[brltty_cv_c_attribute_unused=no])])
if test "${brltty_cv_c_attribute_unused}" = "yes"
then
   AC_DEFINE([HAVE_ATTRIBUTE_UNUSED])
fi

AC_CACHE_CHECK([for system-dependent compilation flags], [brltty_cv_prog_cc_sysflags], [dnl
case "${host_os}"
in
   linux*|gnu*)
      brltty_cv_prog_cc_sysflags="-D_POSIX_C_SOURCE=2 -D_BSD_SOURCE -D_XOPEN_SOURCE=500"
      ;;
   hpux*)
      brltty_cv_prog_cc_sysflags="-D_XOPEN_SOURCE_EXTENDED -D_HPUX_SOURCE"
      ;;
   osf*)
      brltty_cv_prog_cc_sysflags="-D_XOPEN_SOURCE=500 -D_POSIX_C_SOURCE -D_OSF_SOURCE"
      ;;
   *)
      brltty_cv_prog_cc_sysflags=""
      ;;
esac])
SYSCFLAGS="${brltty_cv_prog_cc_sysflags}"
AC_SUBST([SYSCFLAGS])

AC_CACHE_CHECK([for c compiler yacc source flags], [brltty_cv_prog_cc_yaccflags], [dnl
if test "${GCC}" = "yes"
then
   brltty_cv_prog_cc_yaccflags="-Wno-parentheses -Wno-unused -Wno-uninitialized -Wno-unknown-pragmas"
else
   case "${host_os}"
   in
      *)
         brltty_cv_prog_cc_yaccflags=""
         ;;
   esac
fi])
YCFLAGS="${brltty_cv_prog_cc_yaccflags}"
AC_SUBST([YCFLAGS])

AC_CACHE_CHECK([for c compiler shared object flags], [brltty_cv_prog_cc_libflags], [dnl
if test "${GCC}" = "yes"
then
   case "${host_os}"
   in
      cygwin*|mingw*)
         brltty_cv_prog_cc_libflags=""
         ;;
      darwin*)
         brltty_cv_prog_cc_libflags=""
         ;;
      *)
         brltty_cv_prog_cc_libflags="-fPIC"
         ;;
   esac
else
   case "${host_os}"
   in
      *)
         brltty_cv_prog_cc_libflags=""
         AC_MSG_WARN([library flags not configured for ${host_os}])
         ;;
   esac
fi])
AC_SUBST([LIBCFLAGS], ["${brltty_cv_prog_cc_libflags}"])

AC_CACHE_CHECK([for c++ compiler shared object flags], [brltty_cv_prog_cxx_libflags], [dnl
if test "$GXX" = "yes"
then
   brltty_cv_prog_cxx_libflags="-fPIC"
else
   case "${host_os}"
   in
      *)
         brltty_cv_prog_cxx_libflags=""
         ;;
   esac
fi])
LIBCXXFLAGS="${brltty_cv_prog_cxx_libflags}"
AC_SUBST([LIBCXXFLAGS])

AC_SUBST([library_prefix], [lib])
AC_CACHE_CHECK([for dynamic library extension], [brltty_cv_ext_lib], [dnl
case "${host_os}"
in
   hpux*)
      brltty_cv_ext_lib="sl"
      ;;
   cygwin*|mingw*)
      brltty_cv_ext_lib="dll"
      ;;
   darwin*)
      brltty_cv_ext_lib="dylib"
      ;;
   *)
      brltty_cv_ext_lib="so"
      ;;
esac])
AC_SUBST([library_extension], ["${brltty_cv_ext_lib}"])
AC_DEFINE_UNQUOTED([LIBRARY_EXTENSION], ["${library_extension}"])

AC_SUBST([module_name], ["${library_prefix}${PACKAGE_NAME}"])
AC_DEFINE_UNQUOTED([MODULE_NAME], ["${module_name}"])
AC_CACHE_CHECK([for loadable module extension], [brltty_cv_ext_mod], [dnl
case "${host_os}"
in
   darwin*)
      brltty_cv_ext_mod="bundle"
      ;;
   *)
      brltty_cv_ext_mod="${library_extension}"
      ;;
esac])
AC_SUBST([module_extension], ["${brltty_cv_ext_mod}"])
AC_DEFINE_UNQUOTED([MODULE_EXTENSION], ["${module_extension}"])

AC_CACHE_CHECK([for static archive extension], [brltty_cv_ext_arc], [dnl
case "${host_os}"
in
   *)
      brltty_cv_ext_arc="a"
      ;;
esac])
archive_extension="${brltty_cv_ext_arc}"
AC_SUBST([archive_extension])

AC_CACHE_CHECK([for shared object run-time search path environment variable], [brltty_cv_env_libsearch], [dnl
case "${host_os}"
in
   hpux*)
      brltty_cv_env_libsearch="SHLIB_PATH"
      ;;
   darwin*)
      brltty_cv_env_libsearch="DYLD_LIBRARY_PATH"
      ;;
   *)
      brltty_cv_env_libsearch="LD_LIBRARY_PATH"
      ;;
esac])
libsearch_variable="${brltty_cv_env_libsearch}"
AC_SUBST([libsearch_variable])

brltty_pcm_interfaces=""
brltty_midi_interfaces=""
case "${host_os}"
in
   linux*)
      system_object="linux"
      brltty_pcm_interfaces="oss"
      brltty_midi_interfaces="oss"
      AC_CHECK_HEADER([alsa/asoundlib.h], [dnl
         brltty_pcm_interfaces="${brltty_pcm_interfaces} alsa"
         brltty_midi_interfaces="${brltty_midi_interfaces} alsa"
])
      ;;
   gnu*)
      system_object="hurd"
      ;;
   solaris*)
      system_object="solaris"
      ;;
   hpux*)
      system_object="hpux"
      if test -d /opt/audio/include
      then
         SYSCFLAGS="${SYSCFLAGS} -I/opt/audio/include"
         LIBS="-L/opt/audio/lib -lAlib ${LIBS}"
         AC_DEFINE([HAVE_HPUX_AUDIO])
      fi
      ;;
   openbsd*)
      system_object="openbsd"
      ;;
   freebsd*)
      system_object="freebsd"
      ;;
   netbsd*)
      system_object="netbsd"
      ;;
   osf*)
      system_object="osf"
      ;;
   *qnx*)
      system_object="qnx"
      AC_CHECK_LIB([asound], [snd_pcm_open])
      ;;
   cygwin*)
      system_object="cygwin"
      ;;
   mingw*)
      system_object="windows"
      ;;
   darwin*)
      system_object="darwin"
      LIBS="-lIOKit -framework CoreFoundation ${LIBS}"
      ;;
   *)
      AC_MSG_ERROR([no system object available for ${host_os}])
      ;;
esac
system_object="sys_${system_object}."'$O'
AC_SUBST([system_object])

BRLTTY_ARG_DISABLE(
   [preferences-menu],
   [the preferences menu],
   [],
[dnl
   AC_DEFINE([ENABLE_PREFERENCES_MENU])
])

BRLTTY_ARG_DISABLE(
   [table-selection],
   [text/attribute/contraction table selection in preferences menu],
   [],
[dnl
   AC_DEFINE([ENABLE_TABLE_SELECTION])
])
translation_table_objects='tbl.$O'
AC_SUBST([translation_table_objects])

BRLTTY_ARG_DISABLE(
   [learn-mode],
   [interactive command learn mode],
   [],
[dnl
   AC_DEFINE([ENABLE_LEARN_MODE])
])

BRLTTY_ARG_DISABLE(
   [contracted-braille],
   [in-line contracted braille],
   [],
[dnl
   AC_DEFINE([ENABLE_CONTRACTED_BRAILLE])
   contracted_braille_objects='ctb_compile.$O ctb_translate.$O'
   install_contraction_tables=install-contraction-tables
])
AC_SUBST([contracted_braille_objects])
AC_SUBST([install_contraction_tables])

BRLTTY_ARG_DISABLE(
   [beeper-support],
   [support for the console tone generator],
   [],
[dnl
   AC_DEFINE([ENABLE_BEEPER_SUPPORT])
   beeper_tune_objects='beeper.$O'
])
AC_SUBST([beeper_tune_objects])

BRLTTY_ARG_DISABLE(
   [pcm-support],
   [support for soundcard digital audio],
   [${brltty_pcm_interfaces}],
[dnl
   AC_DEFINE([ENABLE_PCM_SUPPORT])
   pcm_tune_objects='pcm.$O'
])
AC_SUBST([pcm_tune_objects])

BRLTTY_ARG_DISABLE(
   [midi-support],
   [support for the Musical Instrument Digital Interface],
   [${brltty_midi_interfaces}],
[dnl
   AC_DEFINE([ENABLE_MIDI_SUPPORT])
   midi_tune_objects='midi.$O'
])
AC_SUBST([midi_tune_objects])

BRLTTY_ARG_DISABLE(
   [fm-support],
   [support for the soundcard synthesizer],
   [],
[dnl
   AC_DEFINE([ENABLE_FM_SUPPORT])
   fm_tune_objects='fm.$O adlib.$O'
])
AC_SUBST([fm_tune_objects])

BRLTTY_ARG_DISABLE(
   [pm-configfile],
   [user customization of Papenmeier driver],
   [],
[dnl
   AC_DEFINE([ENABLE_PM_CONFIGURATION_FILE])
   PM_CONFIGFILE_DEPENDENCY=configuration-file
])
AC_SUBST([PM_CONFIGFILE_DEPENDENCY])

BRLTTY_ARG_ENABLE(
   [gpm],
   [mouse tracking via GPM],
   [],
[dnl
   AC_CHECK_LIB([gpm], [Gpm_Open])
])

AC_CACHE_CHECK([for linkage editor program creation flags], [brltty_cv_prog_ld_flags], [dnl
case "${host_os}"
in
   linux*|gnu*|openbsd*|freebsd*|netbsd*|*qnx*)
      brltty_cv_prog_ld_flags="-export-dynamic -rpath ${execute_root}${library_directory}"
      ;;
   solaris*)
      brltty_cv_prog_ld_flags="-R ${execute_root}${library_directory}"
      ;;
   hpux*)
      brltty_cv_prog_ld_flags="-E +s +b ${execute_root}${library_directory}"
      ;;
   osf*)
      brltty_cv_prog_ld_flags="-rpath ${execute_root}${library_directory}"
      ;;
   cygwin*|mingw*)
      brltty_cv_prog_ld_flags="-export-dynamic -rpath ${execute_root}${library_directory} --enable-auto-import"
      ;;
   darwin*)
      brltty_cv_prog_ld_flags=""
      ;;
   *)
      AC_MSG_ERROR([linkage editor program creation flags not configured for ${host_os}])
      ;;
esac])
brltty_link_flags="${brltty_cv_prog_ld_flags}"

AC_CACHE_CHECK([for static linkage flag], [brltty_cv_prog_ld_static], [dnl
if test "${GCC}" = "yes"
then
   brltty_cv_prog_ld_static="-static"
else
   case "${host_os}"
   in
      linux*|gnu*|openbsd*|freebsd*|*qnx*|cygwin*|mingw*)
         brltty_cv_prog_ld_static="-static"
         ;;
      netbsd*)
         brltty_cv_prog_ld_static="-Bstatic"
         ;;
      solaris*)
         brltty_cv_prog_ld_static="-dn"
         ;;
      hpux*)
         brltty_cv_prog_ld_static="-Bstatic"
         ;;
      osf*)
         brltty_cv_prog_ld_static="-non_shared"
         ;;
      *)
         AC_MSG_ERROR([static linkage not supported by ${host_os}])
         ;;
   esac
   brltty_cv_prog_ld_static="BRLTTY_OPTIONS_LD2CC([${brltty_cv_prog_ld_static}])"
fi])
brltty_static_flag="${brltty_cv_prog_ld_static}"
BRLTTY_ARG_ENABLE(
   [standalone-programs],
   [statically linked executables],
   [],
[dnl
   brltty_standalone_programs="yes"
   LDFLAGS="${LDFLAGS} ${brltty_static_flag}"
])
LDFLAGS="${LDFLAGS} BRLTTY_OPTIONS_LD2CC([${brltty_link_flags}])"

case "${host_os}"
in
   linux*|gnu*|solaris*|openbsd*|freebsd*|netbsd*|osf*|*qnx*|cygwin*|mingw*)
      BRLTTY_SEARCH_LIBS([dlopen], [dl])
      ;;
   hpux*)
      BRLTTY_SEARCH_LIBS([shl_load], [dld])
      ;;
   *)
      ;;
esac

case "${host_os}"
in
   linux*)
      usb_object="linux"
      ;;
   openbsd*)
      usb_object="openbsd"
      ;;
   freebsd*)
      usb_object="freebsd"
      ;;
   netbsd*)
      usb_object="netbsd"
      ;;
   darwin*)
      usb_object="darwin"
      ;;
   *)
      AC_CHECK_HEADERS([usb.h], [usb_object="libusb"])
      ;;
esac
if test -n "${usb_object}"
then
   BRLTTY_ARG_DISABLE(
      [usb-support],
      [support for the Universal Serial Bus],
      [],
[dnl
      AC_DEFINE([ENABLE_USB_SUPPORT])
      case "${usb_object}"
      in
         libusb)
            AC_HAVE_LIBRARY([usb])
            ;;
      esac
      usb_objects="usb."'$O'" usb_${usb_object}."'$O'
])
else
   AC_MSG_WARN([no USB support on ${host_os}])
fi
AC_SUBST([usb_objects])

case "${host_os}"
in
   linux*)
      bluetooth_object="linux"
      ;;
   *)
      bluetooth_object=""
      AC_MSG_WARN([no bluetooth support on ${host_os}])
      ;;
esac
if test -n "${bluetooth_object}"
then
   AC_CHECK_HEADER([bluetooth/bluetooth.h], [dnl
      BRLTTY_ARG_DISABLE(
         [bluetooth-support],
         [support for bluetooth capable devices],
         [],
[dnl
         AC_DEFINE([ENABLE_BLUETOOTH_SUPPORT])
         bluetooth_objects="bluetooth."'$O'" bluetooth_${bluetooth_object}."'$O'
])
])
fi
AC_SUBST([bluetooth_objects])

BRLTTY_BRAILLE_DRIVER([al], [Alva])
BRLTTY_BRAILLE_DRIVER([at], [Albatross])
test -n "${api_objects}" && {
   BRLTTY_BRAILLE_DRIVER([ba], [BrlAPI], [-L$(BLD_TOP)$(PGM_DIR) -lbrlapi])
}
BRLTTY_BRAILLE_DRIVER([bd], [Braudi])
BRLTTY_BRAILLE_DRIVER([bl], [BrailleLite])
BRLTTY_BRAILLE_DRIVER([bm], [Baum])
BRLTTY_BRAILLE_DRIVER([bn], [BrailleNote])
BRLTTY_BRAILLE_DRIVER([cb], [CombiBraille])
BRLTTY_BRAILLE_DRIVER([ec], [EcoBraille])
BRLTTY_BRAILLE_DRIVER([eu], [EuroBraille])
BRLTTY_BRAILLE_DRIVER([fs], [FreedomScientific])
BRLTTY_BRAILLE_DRIVER([ht], [HandyTech])
BRLTTY_IF_PACKAGE([Libbraille], [libbraille], [include/braille.h], [dnl
   BRLTTY_BRAILLE_DRIVER([lb], [Libbraille], [-L$(LIBBRAILLE_ROOT)/lib -lbraille])
])
BRLTTY_BRAILLE_DRIVER([lt], [LogText])
BRLTTY_BRAILLE_DRIVER([mb], [MultiBraille])
BRLTTY_BRAILLE_DRIVER([md], [MDV])
BRLTTY_BRAILLE_DRIVER([mn], [MiniBraille])
BRLTTY_BRAILLE_DRIVER([pm], [Papenmeier])
BRLTTY_BRAILLE_DRIVER([tn], [TechniBraille])
BRLTTY_BRAILLE_DRIVER([ts], [TSI])
BRLTTY_BRAILLE_DRIVER([tt], [TTY])
BRLTTY_BRAILLE_DRIVER([vd], [VideoBraille])
BRLTTY_BRAILLE_DRIVER([vo], [Voyager])
BRLTTY_BRAILLE_DRIVER([vr], [Virtual])
BRLTTY_BRAILLE_DRIVER([vs], [VisioBraille])
test -n "${brltty_package_gui_toolkit}" && {
   BRLTTY_BRAILLE_DRIVER([xw], [XWindow])
}
BRLTTY_ARG_DRIVER([braille], [BrailleDrivers])

AC_CACHE_CHECK([for device directory], [brltty_cv_device_directory], [dnl
case "${host_os}"
in
   cygwin*|mingw*)
      brltty_cv_device_directory=""
      ;;
   *)
      brltty_cv_device_directory="/dev"
      ;;
esac])
AC_DEFINE_UNQUOTED([DEVICE_DIRECTORY], ["${brltty_cv_device_directory}"])

AC_CACHE_CHECK([for first serial device], [brltty_cv_device_serial_first], [dnl
case "${host_os}"
in
   linux*)
      for brltty_device in "ttyS0" "tts/0"
      do
         test -c "${brltty_cv_device_directory}/${brltty_device}" && {
            brltty_cv_device_serial_first="${brltty_device}"
            break
         }
      done
      ;;
   gnu*)
      brltty_cv_device_serial_first="com0"
      ;;
   solaris*)
      brltty_cv_device_serial_first="ttya"
      ;;
   hpux*)
      brltty_cv_device_serial_first="tty0p0"
      ;;
   openbsd*)
      brltty_cv_device_serial_first="cua00"
      ;;
   freebsd*)
      brltty_cv_device_serial_first="cuaa0"
      ;;
   netbsd*)
      brltty_cv_device_serial_first="dty00"
      ;;
   osf*)
      brltty_cv_device_serial_first="tty00"
      ;;
   *qnx*)
      brltty_cv_device_serial_first="ser1"
      ;;
   cygwin*|mingw*)
      brltty_cv_device_serial_first="COM1"
      ;;
   *)
      brltty_cv_device_serial_first=""
      AC_MSG_WARN([no default braille device])
      ;;
esac])
AC_SUBST([first_serial_device], [${brltty_cv_device_serial_first}])

BRLTTY_ARG_WITH(
   [braille-device], [DEVICE],
   [default braille display device],
   [braille_device], ["${brltty_cv_device_serial_first}"]
)
if test "${braille_device}" = "no"
then
   braille_device=""
elif test "${braille_device}" = "yes"
then
   braille_device="${brltty_cv_device_serial_first}"
fi
AC_SUBST([braille_device])
AC_DEFINE_UNQUOTED([BRAILLE_DEVICE], ["${braille_device}"])
BRLTTY_SUMMARY_ITEM([braille-device], [braille_device])

BRLTTY_TEXT_TABLE([cz], [Czech (iso-8859-2)])
BRLTTY_TEXT_TABLE([da], [Danish (iso-8859-1)])
BRLTTY_TEXT_TABLE([de], [German (iso-8859-1)])
BRLTTY_TEXT_TABLE([es], [Spanish (iso-8859-1)])
BRLTTY_TEXT_TABLE([fi1], [Finnish (iso-8859-1)])
BRLTTY_TEXT_TABLE([fi2], [Finnish (iso-8859-1)])
BRLTTY_TEXT_TABLE([fr], [French (iso-8859-1)])
BRLTTY_TEXT_TABLE([fr-cbifs], [Code Braille Informatique Français Standard (iso-8859-1)])
BRLTTY_TEXT_TABLE([it], [Italian (iso-8859-1)])
BRLTTY_TEXT_TABLE([nabcc], [North American Braille Computer Code (iso-8859-1)])
BRLTTY_TEXT_TABLE([no-h], [Norwegian and German (iso-8859-1)])
BRLTTY_TEXT_TABLE([no-p], [Norwegian (iso-8859-1)])
BRLTTY_TEXT_TABLE([pl], [Polish (iso-8859-2)])
BRLTTY_TEXT_TABLE([ru], [Russian (koi8-r)])
BRLTTY_TEXT_TABLE([se1], [Swedish (iso-8859-1)])
BRLTTY_TEXT_TABLE([se2], [Swedish (iso-8859-1)])
BRLTTY_TEXT_TABLE([uk], [United Kingdom English (iso-8859-1)])
BRLTTY_TEXT_TABLE([us], [American English (iso-8859-1)])
BRLTTY_TEXT_TABLE([vni], [Vietnamese (iso-8859-1)])
BRLTTY_ARG_TABLE([text], [nabcc], [text])

BRLTTY_ATTRIBUTES_TABLE([attributes], [])
BRLTTY_ATTRIBUTES_TABLE([attrib], [])
BRLTTY_ARG_TABLE([attributes], [attributes], [])

BRLTTY_ARG_DISABLE(
   [speech-support],
   [support for speech synthesizers],
   [],
[dnl
   AC_DEFINE([ENABLE_SPEECH_SUPPORT])
   speech_support_object='spk.$O'

   BRLTTY_SPEECH_DRIVER([al], [Alva])
   BRLTTY_SPEECH_DRIVER([bl], [BrailleLite])
   BRLTTY_SPEECH_DRIVER([cb], [CombiBraille])
   BRLTTY_SPEECH_DRIVER([es], [ExternalSpeech])
   BRLTTY_IF_PACKAGE([FestivalLite], [flite], [include/flite/flite.h], [dnl
      BRLTTY_ARG_REQUIRED(
         [flite-language], [LANGUAGE],
         [the FestivalLite language to use],
         [flite_language], ["usenglish"]
      )

      BRLTTY_ARG_REQUIRED(
         [flite-lexicon], [LEXICON],
         [the FestivalLite lexicon to use],
         [flite_lexicon], ["cmulex"]
      )

      BRLTTY_ARG_REQUIRED(
         [flite-voice], [VOICE],
         [the FestivalLite voice to use],
         [flite_voice], ["cmu_us_kal16"]
      )

      BRLTTY_SPEECH_DRIVER([fl], [FestivalLite], [-L$(FLITE_ROOT)/lib -lflite_$(FLITE_VOICE) -lflite_$(FLITE_LEXICON) -lflite_$(FLITE_LANGUAGE) -lflite -lm])
   ])
   BRLTTY_SPEECH_DRIVER([fv], [Festival])
   BRLTTY_SPEECH_DRIVER([gs], [GenericSay])
   BRLTTY_IF_PACKAGE([Mikropuhe], [mikropuhe], [mpwrfile.h], [BRLTTY_CAP_PTHREADS([BRLTTY_SPEECH_DRIVER([mp], [Mikropuhe], [-L$(MPLINUX_ROOT) -lmplinux -lm])])])
   BRLTTY_IF_PACKAGE([Swift], [swift], [include/swift.h], [BRLTTY_SPEECH_DRIVER([sw], [Swift], [-L$(SWIFT_ROOT)/lib -lswift -lm])])
   BRLTTY_IF_PACKAGE([Theta], [theta], [include/theta.h], [BRLTTY_SPEECH_DRIVER([th], [Theta], [-L$(THETA_ROOT)/lib -ltheta])])
   BRLTTY_IF_PACKAGE([ViaVoice], [viavoice], [include/eci.h], [BRLTTY_SPEECH_DRIVER([vv], [ViaVoice], [-L$(VIAVOICE_ROOT)/lib -libmeci50])])
])
AC_SUBST([speech_support_object])
BRLTTY_ARG_DRIVER([speech], [SpeechDrivers])

case "${host_os}"
in
   linux*)
      BRLTTY_SCREEN_DRIVER([lx], [Linux])
      ;;
   gnu*)
      BRLTTY_SCREEN_DRIVER([hd], [Hurd])
      ;;
   cygwin*|mingw*)
      BRLTTY_SCREEN_DRIVER([wn], [Windows])
      ;;
   *)
      AC_MSG_WARN([no native screen driver for ${host_os}])
      ;;
esac
BRLTTY_SCREEN_DRIVER([sc], [Screen])

cspi_includes=""
cspi_libdirs=""
cspi_libraries=""
for package in cspi-1.0
do
   pkg-config --exists "${package}" && {
      cspi_includes="`pkg-config --cflags-only-I "${package}"`"
      cspi_libdirs="`pkg-config --libs-only-L "${package}"`"
      cspi_libraries="`pkg-config --libs-only-l "${package}"`"
      BRLTTY_SCREEN_DRIVER([as], [AtSpi], [$(CSPI_LIBDIRS) $(CSPI_LIBRARIES)])
      break
   }
done
AC_SUBST([cspi_includes])
AC_SUBST([cspi_libdirs])
AC_SUBST([cspi_libraries])

BRLTTY_ARG_DRIVER([screen], [ScreenDrivers])
AC_DEFINE_UNQUOTED([SCREEN_DRIVER], ["${brltty_default_code_screen}"])
BRLTTY_SUMMARY_ITEM([screen-driver], [brltty_default_code_screen])

AC_SUBST([install_drivers])
BRLTTY_SUMMARY_END

changequote(, )dnl
brltty_make_files="`echo "${brltty_build_directories}" | sed -e '
s%\([^ ][^ ]*\)%\1/Makefile:prologue.mk.in:\1/Makefile.in%g
'`"
changequote([, ])dnl

AC_OUTPUT([
   config.mk
   brltty.spec
   brltty.lsm
   Programs/brltty-config
   Documents/brltty.conf
   Documents/BrlAPIref.doxy
   BrailleDrivers/Voyager/mkhlp
   ${brltty_make_files}])
